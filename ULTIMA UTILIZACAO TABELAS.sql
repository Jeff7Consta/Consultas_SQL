USE ADCOBMIS01

GO

SELECT
    A.NAME AS [OBJECT_NAME],
    A.TYPE_DESC,
    B.DATABASE_ID,
    C.NAME AS INDEX_NAME,
    (
        SELECT MAX(ULTIMO_ACESSO)
        FROM (VALUES (B.LAST_USER_SEEK),(B.LAST_USER_SCAN),(B.LAST_USER_LOOKUP),(B.LAST_USER_UPDATE)) AS DATAACESSO(ULTIMO_ACESSO)
    ) AS LAST_ACCESS,
    B.LAST_USER_SEEK,
    B.LAST_USER_SCAN,
    B.LAST_USER_LOOKUP,
    B.LAST_USER_UPDATE,
    NULLIF(
        (CASE WHEN B.LAST_USER_SEEK IS NOT NULL THEN 'SEEK, ' ELSE '' END) +
        (CASE WHEN B.LAST_USER_SCAN IS NOT NULL THEN 'SCAN, ' ELSE '' END) +
        (CASE WHEN B.LAST_USER_LOOKUP IS NOT NULL THEN 'LOOKUP, ' ELSE '' END) +
        (CASE WHEN B.LAST_USER_UPDATE IS NOT NULL THEN 'UPDATE, ' ELSE '' END)
    , '') AS OPERATIONS
FROM
    SYS.OBJECTS                                 A
    LEFT JOIN SYS.DM_DB_INDEX_USAGE_STATS       B	ON	B.[OBJECT_ID] = A.[OBJECT_ID] AND B.[DATABASE_ID] = DB_ID()
    LEFT JOIN SYS.INDEXES                       C	ON	C.INDEX_ID = B.INDEX_ID AND C.[OBJECT_ID] = B.[OBJECT_ID]
WHERE
    A.[TYPE_DESC] IN ('VIEW', 'USER_TABLE')
ORDER BY
    A.NAME,
    B.INDEX_ID
